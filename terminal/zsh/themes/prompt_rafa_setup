#!/usr/bin/env zsh

FIRST_CHARACTER_OK="♪"
FIRST_CHARACTER_KO="♫"
DIRTY="✗"
NONE="✓"

prompt_rafa_pwd() {
  prompt_short_dir=$(dot filesystem short_pwd)
  print -n "%F{yellow}${prompt_short_dir}"
}

prompt_rafa_git() {
  [[ -n ${git_info} ]] && print -n "%F{white} on${(e)git_info[prompt]}"
}

prompt_rafa_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_rafa_setup() {
  local prompt_rafa_status='%(?:%F{green}$FIRST_CHARACTER_OK:%F{red}$FIRST_CHARACTER_KO) '

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_rafa_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format '%F{green}%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:clean' format '%F{white}$NONE'
  zstyle ':zim:git-info:dirty' format '%F{yellow}$DIRTY'
  zstyle ':zim:git-info:keys' format 'prompt' ' %F{cyan}%b%c %C%D'

  PS1="${prompt_rafa_status}\$(prompt_rafa_pwd)\$(prompt_rafa_git)%f "
  RPS1=''
}

prompt_rafa_setup "${@}"
